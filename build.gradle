apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.bitsfromspace'
version '1.0-SNAPSHOT'

ext.credentials = new Properties();
credentials.load(file(System.getProperty('user.home') + '/.gradle.credentials').newReader())

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}
signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {


    def ossrhUsername = credentials['ossrhUsername'];
    def ossrhPassword = credentials['ossrhPassword'];

    if (ossrhUsername == null || ossrhPassword == null){
        throw new GradleException("Maven credentials are missing");
    }

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'BFS Utility Casses'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Common-user utility classes'
                url 'https://github.com/chrismaroud/utils'

                scm {
                    url 'https://github.com/chrismaroud/utils'
                    connection 'scm:https://chrismaroud@github.com/chrismaroud/utils.git'
                    developerConnection 'scm:git://github.com/chrismaroud/utils.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chrismaroud'
                        name 'Chris Oudeman'
                        email 'chris@oudeman.com'
                    }
                }
            }
        }
    }
}
